{
  description = "Aaronâ€™s NixOS configurations";

  inputs = {
    nixpkgs.url = "github:nixos/nixpkgs/nixpkgs-25.11-darwin";
    nixpkgs-unstable.url = "github:nixos/nixpkgs/nixpkgs-unstable";

    systems.url = "github:nix-systems/default";

    flake-utils = {
      url = "github:numtide/flake-utils";
      inputs.systems.follows = "systems";
    };

    darwin = {
      url = "github:nix-darwin/nix-darwin/nix-darwin-25.11";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    home-manager = {
      url = "github:nix-community/home-manager/release-25.11";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    neovim-nightly-overlay = {
      url = "github:nix-community/neovim-nightly-overlay";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    jujutsu = {
      url = "github:aaronjsutton/jj?ref=aaron/flake-warning";
      inputs.nixpkgs.follows = "nixpkgs";
      inputs.flake-utils.follows = "flake-utils";
    };

    zig = {
      url = "github:mitchellh/zig-overlay";
      inputs.nixpkgs.follows = "nixpkgs";
      inputs.flake-utils.follows = "flake-utils";
    };
  };

  outputs =
    {
      darwin,
      home-manager,
      nixpkgs,
      self,
      systems,
      ...
    }@inputs:
    let
      overlays = [
        inputs.jujutsu.overlays.default
        inputs.neovim-nightly-overlay.overlays.default
        inputs.zig.overlays.default

        (
          final: prev:
          let
            system = prev.stdenv.hostPlatform.system;
            unstable = import inputs.nixpkgs-unstable {
              inherit system;
            };
          in
          {
            inherit (unstable) gh direnv nil;
          }
        )
      ];

      mkSystem = import ./lib/mksystem.nix {
        inherit
          overlays
          nixpkgs
          inputs
          ;
      };

      eachSystem = nixpkgs.lib.genAttrs (import systems);
    in
    {
      darwinConfigurations.lovelace = mkSystem "macbook-pro-mx" {
        system = "aarch64-darwin";
        user = "aaron";
        darwin = true;
      };

      nixosConfigurations.hammond = mkSystem "dell-precision-3430" {
        system = "x86_64-linux";
        user = "aaron";
      };

      formatter = eachSystem (system: (import nixpkgs { inherit system; }).nixfmt-tree);
    };
}
